project('com.github.rafostar.Clapper', 'c', 'cpp',
  version: '0.3.0',
  meson_version: '>= 0.50.0',
  license: 'GPL-3.0-or-later',
  default_options: [
    'warning_level=1',
    'buildtype=debugoptimized'
  ]
)

gnome = import('gnome')
i18n = import('i18n')
python = import('python')

bindir = join_paths(get_option('prefix'), get_option('bindir'))
libdir = join_paths(get_option('prefix'), get_option('libdir'))
datadir = join_paths(get_option('prefix'), get_option('datadir'))

pkglibdir = join_paths(libdir, meson.project_name())
pkgdatadir = join_paths(datadir, meson.project_name())

if get_option('clapper-lib')
  subdir('lib')
endif

if get_option('clapper-player')
  python_bin = python.find_installation('python3')
  if not python_bin.found()
    error('No valid python3 binary found')
  endif
  subdir('bin')
  subdir('data')
  subdir('src')
  subdir('po')
  meson.add_install_script('build-aux/meson/postinstall.py')
endif
